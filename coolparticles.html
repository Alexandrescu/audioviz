<html>
<head>
<title>Coupled Turing pattern and particle projection feedback |
	WebGL GPGPU</title>
<script id="shader-vs" type="x-shader/x-vertex">
attribute vec3 aPos;
attribute vec2 aTexCoord;
varying   vec2 uv;
varying vec2 uv_orig;
void main(void) {
   gl_Position = vec4(aPos, 1.);
   uv = aTexCoord;
   uv_orig = uv;
}
</script>
<script id="shader-fs-inc" type="x-shader/x-fragment"> 
#ifdef GL_ES
precision mediump float;
#endif

varying vec2 uv;
varying vec2 uv_orig;
		
bool is_onscreen(vec2 uv){
	return (uv.x < 1.) && (uv.x > 0.) && (uv.y < 1.) && (uv.y > 0.);
}

float border(vec2 uv, float border, vec2 texSize){
	uv*=texSize;
	return (uv.x<border || uv.x>texSize.x-border || uv.y<border || uv.y >texSize.y-border) ? 1.:.0;
}

#define pi 3.141592653589793238462643383279
#define pi_inv 0.318309886183790671537767526745
#define pi2_inv 0.159154943091895335768883763372

float border(vec2 domain, float thickness){
   vec2 uv = fract(domain-vec2(0.5));
   uv = min(uv,1.-uv)*2.;
   return clamp(max(uv.x,uv.y)-1.+thickness,0.,1.)/(thickness);
}

float square_mask(vec2 domain){
	return (domain.x <= 1. && domain.x >= 0. && domain.y <= 1. && domain.y >= 0.) ? 1. : 0.; 
}

vec2 complex_mul(vec2 factorA, vec2 factorB){
   return vec2( factorA.x*factorB.x - factorA.y*factorB.y, factorA.x*factorB.y + factorA.y*factorB.x);
}

vec2 spiralzoom(vec2 domain, vec2 center, float n, float spiral_factor, float zoom_factor, vec2 pos){
	vec2 uv = domain - center;
	float d = length(uv);
	return vec2( atan(uv.y, uv.x)*n*pi2_inv + d*spiral_factor, -log(d)*zoom_factor) + pos;
}

vec2 complex_div(vec2 numerator, vec2 denominator){
   return vec2( numerator.x*denominator.x + numerator.y*denominator.y,
                numerator.y*denominator.x - numerator.x*denominator.y)/
          vec2(denominator.x*denominator.x + denominator.y*denominator.y);
}

// HSL to RGB converter code from http://www.gamedev.net/topic/465948-hsl-shader-glsl-code/
float Hue_2_RGB(float v1, float v2, float vH )
{
	float ret;
   if ( vH < 0.0 )
     vH += 1.0;
   if ( vH > 1.0 )
     vH -= 1.0;
   if ( ( 6.0 * vH ) < 1.0 )
     ret = ( v1 + ( v2 - v1 ) * 6.0 * vH );
   else if ( ( 2.0 * vH ) < 1.0 )
     ret = ( v2 );
   else if ( ( 3.0 * vH ) < 2.0 )
     ret = ( v1 + ( v2 - v1 ) * ( ( 2.0 / 3.0 ) - vH ) * 6.0 );
   else
     ret = v1;
   return ret;
}

vec3 hsl2rgb(float H, float S, float L){
	float var_2, var_1, R, G, B;	
	if (S == 0.0)
	{
	   R = L;
	   G = L;
	   B = L;
	}
	else
	{
	   if ( L < 0.5 )
	   {
		   var_2 = L * ( 1.0 + S );
	   }
	   else
	   {
		   var_2 = ( L + S ) - ( S * L );
	   }

	   var_1 = 2.0 * L - var_2;

	   R = Hue_2_RGB( var_1, var_2, H + ( 1.0 / 3.0 ) );
	   G = Hue_2_RGB( var_1, var_2, H );
	   B = Hue_2_RGB( var_1, var_2, H - ( 1.0 / 3.0 ) );
	}
	return vec3(R,G,B);
}

vec2 gradient(sampler2D sampler, vec2 uv, vec2 d, vec4 selector){
	vec4 dX = 0.5*texture2D(sampler, uv + vec2(1.,0.)*d) - 0.5*texture2D(sampler, uv - vec2(1.,0.)*d);
	vec4 dY = 0.5*texture2D(sampler, uv + vec2(0.,1.)*d) - 0.5*texture2D(sampler, uv - vec2(0.,1.)*d);
	return vec2( dot(dX, selector), dot(dY, selector) );
}

vec2 rot90(vec2 vector){
	return vector.yx*vec2(1,-1);
}

vec4 sat(sampler2D sampler_sat, vec2 pos, vec2 size){
	vec4 s = vec4(0);
	s += texture2D(sampler_sat, pos );
	s += texture2D(sampler_sat, pos + size );
	s -= texture2D(sampler_sat, pos + size * vec2(1, 0) );
	s -= texture2D(sampler_sat, pos + size * vec2(0, 1) );
	return s / ( size.x * size.y );
}

vec4 boxblur(sampler2D sampler_sat, vec2 uv, vec2 pixelSize, float boxsize){
	return sat(sampler_sat, uv - boxsize*0.5*pixelSize, boxsize*pixelSize) * pixelSize.x * pixelSize.y;
}

float line_segment(vec2 domain, vec2 p1, float d1, vec2 p2, float d2){
  float h = 1./(p2.x-p1.x); // helper registers
  float h1 = (p2.y-p1.y)*h;
  float h2 = 1./h1;
  float xs = (-p1.y+h1*p1.x+h2*domain.x+domain.y)/(h2+h1);// coordinates of the point on the line between p1 and p2,
  float ys = -h2*(xs-domain.x)+domain.y;					// ^ orthogonally to the given point in the domain
  float d = length(domain-vec2(xs,ys));		// the orthogonal distance from the domain point to the line (unlimited) 
  float s = 0.; // distance from domain point to p1 relative to p2
  if(p2.x == p1.x){	// division by zero fix
    d = abs(domain.x - p1.x);
    s = (p1.y-ys)/(p1.y-p2.y);
  }else{
    s = (xs-p1.x)*h;						 
  }
  d = clamp(d*(d1*(1.-s)+d2*s),0., 1.);	// adjusting the line thickness using a linear interpolation with s
  float m1 = 0.; if(s > 0.) m1 = 1.; 		// masking out the segment between p1 and p2
  float m2 = 0.; if(s < 1.) m2 = 1.;
  float result = clamp( m1*m2-d, 0., 1.); // return result as 1-distance in the range [0..1]
  result = clamp(1.-length(domain-vec2(p1.x,p1.y))*d1-m1, result, 1.);	// round corners if you will (half circles)
  //result = clamp(1.-length(domain-vec2(p2.x,p2.y))*d2-m2, result, 1.);
 	 
  return result;
}

</script>

<script id="shader-fs-copy" type="x-shader/x-fragment"> 
uniform sampler2D source;
void main(void) {
	gl_FragColor = texture2D(source, uv);
}
</script>

<script id="shader-fs-init" type="x-shader/x-fragment"> 
void main(void){
	gl_FragColor = vec4(0.);
}
</script>

<script id="shader-fs-move-particles" type="x-shader/x-fragment"> 
	uniform sampler2D sampler_prev;
	uniform sampler2D sampler_prev_n;
	uniform sampler2D sampler_blur;
	uniform sampler2D sampler_blur2;
	uniform sampler2D sampler_blur3;
	uniform sampler2D sampler_blur4;
	uniform sampler2D sampler_blur5;
	uniform sampler2D sampler_blur6;
	uniform sampler2D sampler_noise;
	uniform sampler2D sampler_noise_n;
	uniform sampler2D sampler_fluid;
	uniform sampler2D sampler_particles;

	uniform vec2 texSize;
	uniform vec2 pixelSize;
	uniform vec2 scale;
	uniform vec4 rnd;
	uniform float frame;
	uniform vec2 mouse;
	uniform vec2 mouseV;

void main(void){
	vec4 p = texture2D(sampler_particles, uv); // location and velocity of the particle in the previous frame
	vec2 f = texture2D(sampler_fluid, p.xy).xz*pixelSize; // fluid simulation flow vector

	p.zw += gradient(sampler_blur6, p.xy, pixelSize*48., vec4(0,1.5,0.,0.))*pixelSize; // add gradient force to velocity
	p.zw = p.zw * max(0., 1.- (p.z*p.z + p.w*p.w)*20.); // apply friction
	p.xy += p.zw; // verlet integration

	p.xy += rot90(gradient(sampler_blur2, p.xy, pixelSize*8., vec4(-0.5,0,0.,0.)))*pixelSize; // rotation around the red dots

	p.xy += f; // fluid

	gl_FragColor.xy = fract(p.xy); // wrap

	gl_FragColor.zw = p.zw; 
}
</script>

<script id="shader-fs-composite" type="x-shader/x-fragment"> 
	uniform sampler2D sampler_prev;
	uniform sampler2D sampler_prev_n;
	uniform sampler2D sampler_blur;
	uniform sampler2D sampler_blur2;
	uniform sampler2D sampler_blur3;
	uniform sampler2D sampler_blur4;
	uniform sampler2D sampler_blur5;
	uniform sampler2D sampler_blur6;
	uniform sampler2D sampler_noise;
	uniform sampler2D sampler_noise_n;
	uniform sampler2D sampler_fluid;
	uniform sampler2D sampler_fluid_p;
	uniform sampler2D sampler_particles;
	uniform sampler2D sampler_particle_projection;
	uniform sampler2D sampler_sat;

	uniform vec4 rnd;
	uniform vec4 rainbow;
	uniform vec2 texSize;
	uniform vec2 pixelSize;
	uniform vec2 aspect;
	uniform vec2 mouse;
	uniform vec2 mouseV;
	uniform float fps;
	uniform float time;
	uniform float frame;

float mouseFilter(vec2 uv){
	return clamp( 1.-length((uv-mouse)*texSize)/16., 0. , 1.);
}
void main(void) {
	gl_FragColor.x = texture2D(sampler_prev, uv).x;
	gl_FragColor = texture2D(sampler_prev, uv);
//	gl_FragColor.y = texture2D(sampler_particle_projection, uv).x;
	gl_FragColor.z = mouseFilter(uv);
	gl_FragColor.a = 1.;
}
</script>
<script id="shader-fs-advance" type="x-shader/x-fragment"> 
	uniform sampler2D sampler_prev;
	uniform sampler2D sampler_prev_n;
	uniform sampler2D sampler_blur;
	uniform sampler2D sampler_blur2;
	uniform sampler2D sampler_blur3;
	uniform sampler2D sampler_blur4;
	uniform sampler2D sampler_blur5;
	uniform sampler2D sampler_blur6;
	uniform sampler2D sampler_noise;
	uniform sampler2D sampler_noise_n;
	uniform sampler2D sampler_fluid;
	uniform sampler2D sampler_particles;
	uniform sampler2D sampler_particle_projection;

	uniform vec4 rnd;
	uniform vec4 rainbow;
	uniform vec2 pixelSize;
	uniform vec2 aspect;
	uniform vec2 mouse;
	uniform vec2 mouseV;
	uniform float fps;
	uniform float time;
	uniform float frame;

void main(void) {
	float noise = texture2D(sampler_noise, uv + rnd.zw).x*0.5-0.25;
	vec2 f = texture2D(sampler_fluid, uv).xz*pixelSize;

	gl_FragColor = vec4(0.);

	//starting from a simple Turing pattern in red

	vec2 uvr = uv + noise*pixelSize*0.5 - f;
	uvr +=  gradient(sampler_blur4, uv, pixelSize*16., vec4(0.,-2.,0.,0.))*pixelSize; // here we add some gradient displacement mapping
	gl_FragColor.r = texture2D(sampler_prev, uvr).r;
	gl_FragColor.r += (texture2D(sampler_blur2, uv).r - texture2D(sampler_blur3, uvr).r)*12./256. - 1./256.;

	// then there's the particle projection that's added to the green

	gl_FragColor.g = texture2D(sampler_blur2, uv).g *240./256.;
	gl_FragColor.g = mix(gl_FragColor.g, texture2D(sampler_particle_projection, uv).g, 16./256.);

	
	gl_FragColor = clamp(gl_FragColor, 0., 1.);
	gl_FragColor.a = 1.;
}
</script>

<script id="shader-fs-sat" type="x-shader/x-fragment">
uniform sampler2D sampler_sat; // the texture that is also being rendered to
uniform vec2 offset; // add current value to the other pixel, variable for the two passes (rows, columns) 
void main(){
	gl_FragColor = texture2D(sampler_sat, uv) + texture2D(sampler_sat, uv + offset);
}
</script>

<script id="shader-fs-blur-horizontal" type="x-shader/x-fragment">
// original shader from http://www.gamerendering.com/2008/10/11/gaussian-blur-filter-shader/
// horizontal blur fragment shader
uniform sampler2D src_tex;
uniform vec2 pixelSize;

void main(void) // fragment
{
	float h = pixelSize.x;
	vec4 sum = vec4(0.0);
	sum += texture2D(src_tex, vec2(uv.x - 4.0*h, uv.y) ) * 0.05;
	sum += texture2D(src_tex, vec2(uv.x - 3.0*h, uv.y) ) * 0.09;
	sum += texture2D(src_tex, vec2(uv.x - 2.0*h, uv.y) ) * 0.12;
	sum += texture2D(src_tex, vec2(uv.x - 1.0*h, uv.y) ) * 0.15;
	sum += texture2D(src_tex, vec2(uv.x + 0.0*h, uv.y) ) * 0.16;
	sum += texture2D(src_tex, vec2(uv.x + 1.0*h, uv.y) ) * 0.15;
	sum += texture2D(src_tex, vec2(uv.x + 2.0*h, uv.y) ) * 0.12;
	sum += texture2D(src_tex, vec2(uv.x + 3.0*h, uv.y) ) * 0.09;
	sum += texture2D(src_tex, vec2(uv.x + 4.0*h, uv.y) ) * 0.05;
    gl_FragColor.xyz = sum.xyz/0.98; // normalize
	gl_FragColor.a = 1.;
} 
</script>

<script id="shader-fs-blur-vertical" type="x-shader/x-fragment">
uniform sampler2D src_tex;
uniform vec2 pixelSize;

void main(void) // fragment
{
	float v = pixelSize.y;
	vec4 sum = vec4(0.0);
	sum += texture2D(src_tex, vec2(uv.x, - 4.0*v + uv.y) ) * 0.05;
	sum += texture2D(src_tex, vec2(uv.x, - 3.0*v + uv.y) ) * 0.09;
	sum += texture2D(src_tex, vec2(uv.x, - 2.0*v + uv.y) ) * 0.12;
	sum += texture2D(src_tex, vec2(uv.x, - 1.0*v + uv.y) ) * 0.15;
	sum += texture2D(src_tex, vec2(uv.x, + 0.0*v + uv.y) ) * 0.16;
	sum += texture2D(src_tex, vec2(uv.x, + 1.0*v + uv.y) ) * 0.15;
	sum += texture2D(src_tex, vec2(uv.x, + 2.0*v + uv.y) ) * 0.12;
	sum += texture2D(src_tex, vec2(uv.x, + 3.0*v + uv.y) ) * 0.09;
	sum += texture2D(src_tex, vec2(uv.x, + 4.0*v + uv.y) ) * 0.05;
    gl_FragColor.xyz = sum.xyz/0.98;
	gl_FragColor.a = 1.;
}
</script>

<script id="shader-fs-add-mouse-motion" type="x-shader/x-fragment"> 
uniform sampler2D sampler_fluid;

uniform vec2 aspect;
uniform vec2 mouse; // mouse coordinate
uniform vec2 mouseV; // mouse velocity
uniform vec2 pixelSize;
uniform vec2 texSize;

float mouseFilter(vec2 uv){
	return clamp( 1.-length((uv-mouse)*texSize)/2., 0. , 1.);
}

void main(void){
	vec2 v = texture2D(sampler_fluid, uv).xz;

	if(length(mouseV) > 0.)
		v = mix(v, mouseV*2., mouseFilter(uv)*0.85);

	gl_FragColor.xz = v;
}

</script>

<script id="shader-fs-advect" type="x-shader/x-fragment"> 
uniform vec2 texSize;
uniform vec2 pixelSize;

uniform sampler2D sampler_fluid;

const float dt = .0005;

void main(void){
	vec2 v = texture2D(sampler_fluid, uv).xz;

	vec2 D = -texSize*vec2(v.x, v.y)*dt;

   vec2 Df = floor(D),   Dd = D - Df;
   vec2 uv = uv + Df*pixelSize;

	vec2 uv0, uv1, uv2, uv3;

	uv0 = uv + pixelSize*vec2(0.,0.);
	uv1 = uv + pixelSize*vec2(1.,0.);
	uv2 = uv + pixelSize*vec2(0.,1.);
	uv3 = uv + pixelSize*vec2(1.,1.);

	vec2 v0 = texture2D(sampler_fluid, uv0).xz;
	vec2 v1 = texture2D(sampler_fluid, uv1).xz;
	vec2 v2 = texture2D(sampler_fluid, uv2).xz;
	vec2 v3 = texture2D(sampler_fluid, uv3).xz;

	v = mix( mix( v0, v1, Dd.x), mix( v2, v3, Dd.x), Dd.y);

	gl_FragColor.xz = v*(1.-border(uv, 1., texSize));
}

</script>

<script id="shader-fs-p" type="x-shader/x-fragment"> 
uniform vec2 pixelSize;
uniform vec2 texSize;
uniform sampler2D sampler_v;
uniform sampler2D sampler_p;
const float h = 1./1024.;

void main(void){

	vec2 v = texture2D(sampler_v, uv).xz;
	float v_x = texture2D(sampler_v, uv - vec2(1.,0.)*pixelSize).r;
	float v_y = texture2D(sampler_v, uv - vec2(0.,1.)*pixelSize).b;

	float n = texture2D(sampler_p, uv- pixelSize*vec2(0.,1.)).r;
	float w = texture2D(sampler_p, uv + pixelSize*vec2(1.,0.)).r;
	float s = texture2D(sampler_p, uv + pixelSize*vec2(0.,1.)).r;
	float e = texture2D(sampler_p, uv - pixelSize*vec2(1.,0.)).r;

	float p = ( n + w + s + e - (v.x - v_x + v.y - v_y)*h ) * .25;

	gl_FragColor.r = p;
	gl_FragColor.ba = vec2(0.); // unused
}
</script>

<script id="shader-fs-div" type="x-shader/x-fragment"> 
uniform vec2 texSize;
uniform vec2 pixelSize;
uniform sampler2D sampler_v;
uniform sampler2D sampler_p;

void main(void){
	float p = texture2D(sampler_p, uv).r;
	vec2 v = texture2D(sampler_v, uv).xz;
	float p_x = texture2D(sampler_p, uv + vec2(1.,0.)*pixelSize).r;
	float p_y = texture2D(sampler_p, uv + vec2(0.,1.)*pixelSize).r;

	v -= (vec2(p_x, p_y)-p)*512.;

	gl_FragColor.xz = v;
}
</script>

<script type="x-shader/x-vertex" id="shader-particle-renderer-vs"> 
attribute vec2 uv; // particle position lookup vector
uniform sampler2D sampler_particles; // particle positions in a float texture
uniform vec2 mouse;
			
void main() {
	gl_Position = (texture2D(sampler_particles, uv) - 0.5)*2.; // pass em flat
	gl_Position.zw = vec2(0,1);
	gl_PointSize = 1.;	
}
</script>
<script type="x-shader/x-fragment" id="shader-particle-renderer-fs"> 
void main() {
	gl_FragColor = vec4(1.0, 0.5, 0.166, 0.33);
}
</script>


<script src="./experimental/main.js"></script>

<style type="text/css">
body {
	background-color: #000000;
	color: #FFFFFF;
}

#c {
	position: absolute;
	top: 0;
	left: 0;
	z-index: -1;
}

a {
	color: #FFFFFF;
	font-weight: bold;
}

#desc {
	background-color: rgba(0, 0, 0, 0.2);
	width: 1024;
}
</style>
</head>
<body onload="load()" ondblclick="hide()">
	<div id="desc">
		Coupled Turing pattern and 2<sup>19</sup> particles in a projection
		feedback loop with Gaussian blur gradient flow and fluid simulation.
		fps: <span id="fps"></span>
		<form>
			<!-- 
			limit fps <input type="text" name="desiredFPS" size="3" value="50"
				onKeyUp="setDesiredFps(this)"></input> 
		 -->
			<br> <input type="radio" name="render"
				onclick="switchRenderer(false)">raw </input> <br> <input
				type="radio" name="render" onclick="switchRenderer(true)"
				checked="checked">points </input> <br> <input type="checkbox"
				onclick="goFull(this)" checked="checked">full </input>
		</form>
	</div>
	<canvas id="c"></canvas>
</body>
</html>
